#	pim1.g4bl
#
#	Mue4 PSI g4 beam line model for the HIMB project 
#
#	author: L. Desorgher
#   History:
#       Revised to match Turtle input deck the P Reimer (PER)

param -unset firstEvent=1
param -unset nEvents=10000
param -unset beamPart=mu+
# PER 2 Sept 2018 The Turtle deck that I copied the magnet parameters from was documented to say that the beamline momentum was 155 MeV
param -unset P=155
param -unset fudge=1.0

param file=MuSEBeam_$beamPart$P$firstEvent
param histoFile=$file.root

param outFile=$file.out
#output $outFile

# PER 2 Sept 2018 For beamline momentum other than 155, we scale current in the magnets
param scale=$P/155
param P=abs($P)

# PER 2 Sept 2018 basically I did a survey of what people recommended that I use for the physics engine, and the overwelming reccomendation was qgsp_bert_hp
#param physicsName=FTFP_BERT
#param physicsName=QGSP_BERT
param -unset physicsName=QGSP_BERT_HP
physics $physicsName

#primary particles
param partName=$beamPart
param tuneP=$P

param -unset beamAngle=22.5
#param P=590
param sigmaX=1.5
param sigmaY=1.5
param sigmaZ=0. 
//param sigmaXp=0.03
//param sigmaYp=0.08
// since the first apeture is at approx 1 m and it is 7.5 cm radius, no beam with dx/dz > 0.075 will be accepted
param sigmaXp=0.03
param sigmaYp=0.08
param sigmaP=$P*0.02

param zProdTarget=-948
param startZ=$zProdTarget

#convert kG/mm to T/m
param kGpmm_to_Tpm=100.0
#quad parameter RIRON
param RIRON=800


param rmin_pipe=500
param rmax_pipe=700

param killInPipe=0
param pipeColor=0,1,1,0.4
#param killInIron=0
param killInIron=1
param ironColor=1,0,0,0.4
param absorbAllColor=1,1,0,0.05
param killAllColor=$absorbAllColor
param -unset killInShield=1
param shieldColor=$killAllColor

particlecolor e+=1,1,1 mu+=0,1,1 

collective
param worldMaterial=Vacuum
param maxstep=10 deltaChord=0.5
start x=0. y=0. z=0. initialZ=$startZ
#Adjust centerline to 22 degrees
cornerarc z=$zProdTarget angle=$beamAngle centerRadius=10
beam gaussian beamZ=$startZ particle=$beamPart nEvents=$nEvents firstEvent=$firstEvent meanMomentum=$P sigmaP=$sigmaP sigmaX=$sigmaX sigmaY=$sigmaY sigmaZ=$sigmaZ sigmaXp=$sigmaXp sigmaYp=$sigmaYp


# define a generic detector plane
param MuSEDetColor=1,0,1,1
param MuSEDetLength=15
param MuSEDetRadius=1.5*$rmin_pipe
virtualdetector MuSEDet radius=$MuSEDetRadius length=$MuSEDetLength color=$MuSEDetColor

# define QSL quad
param LQSL=726
param RQSL=120
genericquad QSL fieldLength=$LQSL coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQSL kill=$killInIron fringe=0 ironColor=$ironColor

#Define shielding tube
tubs SHIELD innerRadius=$RIRON*1.1 outerRadius=$RIRON*1.2 kill=$killInShield color=$shieldColor

########################################
#Magnetic field maps and model definitions
###########################################

param ASM_map=ASM.g4blmap
fieldmap ASM file=$ASM_map

####################
#Beam elements
#####################

place SHIELD z=$startZ-100 length=10 innerRadius=0 outerRadius=$RIRON*1.1 rename=SheildStart

place MuSEDet z=$startZ+$MuSEDetLength rename=Det0


place MuSEDet z=-$MuSEDetLength rename=Det0a
param LDRIFT1=0
param PDRIFT1=$LDRIFT1/2

param LQTA1=324
param RQTA1=75.0
param BQTA1=0.9854*$scale
param GradQTA1=$BQTA1/$LQTA1*$kGpmm_to_Tpm
param RIRON=500
genericquad QTA11 fieldLength=$LQTA1 apertureRadius=$RQTA1 gradient=$GradQTA1 ironRadius=$RIRON ironLength=$LQTA1 kill=$killInIron fringe=0 ironColor=$ironColor
param PQTA1=$LDRIFT1+0.5*$LQTA1
place QTA11 z=$PQTA1

param LDRIFT2=100
param PDRIFT21=$PQTA1+0.5*$LQTA1+0.5*$LDRIFT2

param LQTB=475
param RQTB=115.0
param BQTB1=1.0894*$scale
param GradQTB1=$BQTB1/$LQTB*$kGpmm_to_Tpm
genericquad QTB11 gradient=$GradQTB1 fieldLength=$LQTB coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQTB kill=$killInIron fringe=0 ironColor=$ironColor
param PQTB1=$PDRIFT21+0.5*$LDRIFT2+0.5*$LQTB
place QTB11 z=$PQTB1

param PDRIFT22=$PQTB1+0.5*$LQTB+0.5*$LDRIFT2

param BQTB2=-1.1965*$scale
param GradBQTB2=$BQTB2/$RQTB*$kGpmm_to_Tpm
genericquad QTB12 gradient=$GradBQTB2 fieldLength=$LQTB coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQTB kill=$killInIron fringe=0 ironColor=$ironColor
param PQTB2=$PDRIFT22+0.5*$LDRIFT2+0.5*$LQTB
place QTB12 z=$PQTB2

param LDRIFT3=3586
param PDRIFT3=$PQTB2+0.5*$LQTB+0.5*$LDRIFT3

param LDRIFT4=1334
param PDRIFT4=$PDRIFT3+0.5*$LDRIFT3+0.5*$LDRIFT4

place SHIELD rename=SHEILD_A z=$PDRIFT4/2-250 length=$PDRIFT4+2500

place MuSEDet z=$PDRIFT4+0.5*$LDRIFT4+0.75*$MuSEDetLength rename=Det1


param ANGLE_ASM=75.
param ANGLE_ASM_deg=$ANGLE_ASM*pi/180.
param L_center_line_ASM=2.*775.39
cornerarc z=$PDRIFT4+0.5*$LDRIFT4 angle=$ANGLE_ASM/2. centerRadius=$L_center_line_ASM/$ANGLE_ASM_deg
param PASM1=$PDRIFT4+0.5*$LDRIFT4+0.5*$L_center_line_ASM

place SHIELD length=950 z=$PASM1 innerRadius=3*$RIRON outerRadius=3.1*$RIRON initialPhi=-60 finalPhi=60

#The map is set to 1Tesla so the current/intensity gives the strength of the dipole in Tesla 
param BASM1=-0.591271616286*155.0/210.0*$scale*$fudge
place ASM rename=ASM1 z=$PASM1 current=$BASM1  
cornerarc z=$PASM1 angle=$ANGLE_ASM/2. centerRadius=$L_center_line_ASM/$ANGLE_ASM_deg

place SHIELD rename=sheild_bend z=$PDRIFT4+$L_center_line_ASM length=4000  innerRadius=2.0*$RIRON outerRadius=2.1*$RIRON initialPhi=65 finalPhi=295

param LDRIFT5=1352
param PDRIFT5=$PASM1+0.5*$L_center_line_ASM+0.5*$LDRIFT5

param beginCenter=$PASM1++0.5*$L_center_line_ASM

place MuSEDet z=$PDRIFT5-0.5*$LDRIFT5-0.75*$MuSEDetLength rename=Det2

param BQSL11=1.2731*$scale
param GradBQSL11=$BQSL11/$RQSL*$kGpmm_to_Tpm
param PQSL11=$PDRIFT5+0.5*$LDRIFT5+0.5*$LQSL
place QSL rename=QSL11 gradient=$GradBQSL11 z=$PQSL11

param LDRIFT6=100
param PDRIFT6=$PQSL11+0.5*$LQSL+0.5*$LDRIFT6

param BQSL12=-0.9086*$scale
param GradBQSL12=$BQSL12/$RQSL*$kGpmm_to_Tpm
param PQSL12=$PDRIFT6+0.5*$LDRIFT6+0.5*$LQSL
place QSL rename=QSL12 gradient=$GradBQSL12 z=$PQSL12

param LDRIFT7=739-0.5*$MuSEDetLength
param PDRIFT7=$PQSL12+0.5*$LQSL+0.5*$LDRIFT7

place MuSEDet z=$PDRIFT7+0.5*$LDRIFT7+0.5*$MuSEDetLength rename=Det3

param LDRIFT8=$LDRIFT7
param PDRIFT8=$PDRIFT7+$MuSEDetLength+0.5*$LDRIFT7+0.5*$LDRIFT8

param BQSL13=-0.9086*$scale
param GradBQSL13=$BQSL13/$RQSL*$kGpmm_to_Tpm
param PQSL13=$PDRIFT8+0.5*$LDRIFT8+0.5*$LQSL
place QSL rename=QSL13 gradient=$GradBQSL13 z=$PQSL13

param LDRIFT9=100
param PDRIFT9=$PQSL13+0.5*$LQSL+0.5*$LDRIFT9

param BQSL14=1.2732*$scale
param GradBQSL14=$BQSL14/$RQSL*$kGpmm_to_Tpm
param PQSL14=$PDRIFT9+0.5*$LDRIFT9+0.5*$LQSL
place QSL rename=QSL14 gradient=$GradBQSL14 z=$PQSL14

param LDRIFT10=1352
param PDRIFT10=$PQSL14+0.5*$LQSL+0.5*$LDRIFT10

place MuSEDet z=$PDRIFT10+0.5*$LDRIFT10+0.51*$MuSEDetLength rename=Det4

param endCenter=$PDRIFT10+0.5*$LDRIFT10

place SHIELD rename=SHIELD_B z=($endCenter+$beginCenter)/2.0 length=$endCenter-$beginCenter

cornerarc z=$PDRIFT10+0.5*$LDRIFT10 angle=$ANGLE_ASM/2. centerRadius=$L_center_line_ASM/$ANGLE_ASM_deg
param PASM2=$PDRIFT10+0.5*$LDRIFT10+0.5*$L_center_line_ASM
#The map is set to 1Tesla so the current/intensity gives the strength of the dipole in Tesla 
param BASM2=-0.591271616286*155.0/210.0*$scale*$fudge
place ASM rename=ASM2 z=$PASM2 current=$BASM2  
cornerarc z=$PASM2 angle=$ANGLE_ASM/2. centerRadius=$L_center_line_ASM/$ANGLE_ASM_deg

place MuSEDet z=1478+$PASM2 rename=Det5

param BQSL15=-0.5440*$scale
param GradBQSL15=$BQSL15/$RQSL*$kGpmm_to_Tpm
place QSL rename=QSL15 z=2940-1452/2.0-$LQSL/2+$PASM2

param BQSL16=0.7506*$scale
param GradBQSL16=$BQSL16/$RQSL*$kGpmm_to_Tpm
place QSL rename=QSL16 gradient=$GradBQSL16 z=2940-$LQSL/2+$PASM2

param BQSL17=-0.9086*$scale
param GradBQSL17=$BQSL17/$RQSL*$kGpmm_to_Tpm
place QSL rename=QSL17 gradient=$GradBQSL17 z=4231.25+$PASM2

param BQSL18=1.2732*$scale
param GradBQSL18=$BQSL18/$RQSL*$kGpmm_to_Tpm
place QSL rename=QSL18 gradient=$GradBQSL18 z=4957.75+$PASM2

param targetLocation=6802.678+$PASM2

place MuSEDet z=$targetLocation-1000 rename=DetBH

place MuSEDet z=$targetLocation rename=DetTar

place SHIELD rename=SHIELD_TAR z=$targetLocation innerRadius=2*$MuSEDetRadius outerRadius=2.1*$MuSEDetRadius length=5000

place MuSEDet z=$targetLocation+1500 rename=DetBC1

place MuSEDet z=$targetLocation+2500 rename=DetBC2

ntuple partOut detectors=Det* require=DetTar
