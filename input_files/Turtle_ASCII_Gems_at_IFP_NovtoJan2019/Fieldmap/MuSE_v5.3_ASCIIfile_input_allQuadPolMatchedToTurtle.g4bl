#	pim1.g4bl
#
#	Mue4 PSI g4 beam line model for the HIMB project 
#
#	author: L. Desorgher
#   History:
#       Revised to match Turtle input deck the P Reimer (PER)
#	Revised to match Turtle by Priya, Univ. of Michigan (PRoy)
#	uses sector bend Priya 
#
#	PER 23 Oct 2018
#	    seperate pChannel pOffset and pBeam PER
#	    removed fudge
#	    added "sepChar" to filename
#	    added fieldMap parameter if fieldMap==1, them read in ASM1.g4blmap and ASM2.g4blmap,
#	    	  otherwise use sector bend magnets

#     PRoy Nov 2, 2018
#     Polarities of Quads reversed compared to Turtle in 1st & 3rd segments of beamline. 
#     Polarities match Turtle in the 2nd segment of the beamline. 

#     PRoy Nov 4, 2018
#     Not comfortable with tuning polarities of quads before improving the dipoles. So, made
#     polarities match Turtle again in all segments of the beamline. 

#     PRoy Nov 8, 2018
#     Define flat muon beam to match Ron's flat source muon beam in turtle with +/- 1 mm spread 
#     in x,y and +/- 0.5 mrad angular spread, 1.5% momentum dispersion. 
#     Compare dispersion plots at dipoles, IFP and target. 

#     PRoy Nov 9, 2018
#     Change field signs of the quads to match transport model from Turtle. Assume Turtle model is for pi+.
#     Pg 62 g4beamline guide says +ve quad gradient yields horizontally focused quad for +ve particles.

#     PRoy Nov 10, 2018
#     Place virtual detectors before QTA1, between QTA1 and QTB1, between QTB1 and QTB2, at exit of QTB2. 
#     Compare dispersion at these places with Turtle

#     PRoy Nov 19, 2018
#     Use Turtle's input textfile as beam source.  

#     PRoy Nov 23, 2018
#     Removed 'fringe=0' from quads to add the default fringe effects provided by g4beam.

###################################################################################
## PRoy: See 'http://aea.web.psi.ch/beam2lines/beam_pim1.html' for PiM1 beamline properties
###################################################################################

param -unset firstEvent=1
param -unset nEvents=100000
param -unset lastEvent=100000
param -unset beamPart=mu+

# PER 2 Sept 2018 The Turtle deck that I copied the magnet parameters from was documented
# PER 2 Sept 2018 to say that the beamline momentum was 155 MeV
# PER 23 Oct 2018 Changed P to pChannel to distinguish from beam momentum.  Propogated this throughout the file

param -unset pChannel=155

# PER 23 Oct 2018 Allow the user to specify a different momentum
param -unset pOffset=1.0
#param -unset pBeam=$pChannel*$pOffset

# PER 2 Sept 2018 For beamline momentum other than 155, we scale current in the magnets
param scale=$pChannel/155


param -unset fieldMap=1
param -unset map1=ASM1.g4blmap
param -unset map2=ASM2.g4blmap
# PER & PRoy  Add ability to scale magnets sets
# dipole scale determined empirically for use with ASM1 and ASM2
param -unset scaleDipole=1.000295
param -unset scaleQSL=1
param -unset scaleQT=1

param sepChar="_"
param file=MuSEBeam_$beamPart$sepChar$pChannel$sepChar$pOffset
param histoFile=$file.root

# PER 2 Sept 2018 Basically I did a survey of what people recommended that I use 
# PER 2 Sept 2018 for the physics engine, and the overwelming reccomendation was qgsp_bert_hp
#param physicsName=FTFP_BERT
#param physicsName=QGSP_BERT
param -unset physicsName=QGSP_BERT_HP
physics $physicsName

#primary particles
param partName=$beamPart
param tuneP=abs($pChannel)

param -unset beamAngle=22.5

param zProdTarget=-958
param startZ=$zProdTarget

#convert kG/mm to T/m
param kGpmm_to_Tpm=100

#PRoy (Nov 4, 2018) convert kG/mm^2 to T/m^2 (for sextupole moment)
param kGpmm2_to_Tpm2=100000

#quad parameter RIRON
param RIRON=800

param killInPipe=0
param pipeColor=0,1,1,0.4
param killInIron=1
param ironColor=1,0,0,0.4
param absorbAllColor=1,1,0,0.05
param killAllColor=$absorbAllColor
param -unset killInShield=1
param shieldColor=$killAllColor

particlecolor e+=1,1,1 e-=1,1,1 mu+=0,1,1 mu-=0,1,1

#PRoy 20 Sept 2018: track cuts to keep only mu+ and e+
#trackcuts keep=mu+,e+,pi+,pi0,mu-,e-,pi-
trackcuts keep=mu+

#PRoy: 111 is white, 011 is light blue
#PRoy: by default, red for negative particles, green for neutrals 

collective
param worldMaterial=Vacuum
param maxstep=10 deltaChord=0.5
start x=0. y=0. z=0. initialZ=$startZ

#Adjust centerline to 22 degrees

cornerarc z=$zProdTarget angle=$beamAngle centerRadius=10
beam ASCII file=g4_in.txt

###define a generic detector plane (i.e. the magenta planes which represent virtual detectors)

param MuSEDetColor=1,0,1,1 
#(this gives the magenta color to the detector plane)

param MuSEDetLength=15
param MuSEDetRadius=500
virtualdetector MuSEDet radius=$MuSEDetRadius length=$MuSEDetLength color=$MuSEDetColor

### define QSL quad (i.e. quadrupole magnet properties, the magnets are shown as red cylinders)
### The 'QSL' quad is defined here such that it has rounded + aperture. See pg 40 of user manual.

#PRoy 3 Oct 2018: corrected LQSL to 590 mm to match Turtle
param LQSL=590
param RQSL=120
genericquad QSL fieldLength=$LQSL coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQSL kill=$killInIron ironColor=$ironColor

#Define shielding tube
tubs SHIELD innerRadius=$RIRON*1.1 outerRadius=$RIRON*1.2 kill=$killInShield color=$shieldColor

param LPole=10
param RPole=160

if "$fieldMap==1"
#    PER (23 Oct 2018)field maps
#    read in field for dipoles
     fieldmap ASM1 file=$map1
     fieldmap ASM2 file=$map2
else
#    ##### P Roy (Oct 10, 2018): define a bending magnet *********************************************
     idealsectorbend BendType1 angle=75 fieldCenterRadius=1185 fieldInnerRadius=1025 fieldOuterRadius=1345 fieldHeight=72 ironColor=1,0,0,0.2 ironInnerRadius=725 ironOuterRadius=1645 ironHeight=572 kill=1

     ##### P Roy (Nov 2, 2018): define short quadrupole magnets & place at the faces of the dipoles***

     multipole Pole fieldLength=$LPole ironLength=$LPole ironRadius=572 apertureRadius=$RPole ironColor=0,1,1 kill=1 fringe=0 
     
endif

####################
#Beam elements
#####################

place SHIELD z=$startZ-100 length=10 innerRadius=0 outerRadius=$RIRON*1.1 rename=SheildStart

place MuSEDet z=$startZ+$MuSEDetLength rename=Det0 
#this places the first virtual detector

place MuSEDet z=-$MuSEDetLength rename=Det0a 
#this places a virtual detector at start of quad QTA11

#*****Define and place Quad magnet named QTA11 with circular aperture ****
### define properties of quad magnet with circular aperture

param LDRIFT1=0
param PDRIFT1=$LDRIFT1/2

param LQTA1=324 
#length magnet in mm

param RQTA1=75.0 
#radius aperture in mm

# PRoy (Nov 9, 2018): assume turtle transport model pic from is for pi+.
# QTA1 defocuses focuses pi+ in x. i.e. gradient should be -ve (pg 62, g4beamline manual).

param BQTA1=-0.9854*$scale*$scaleQT
param GradQTA1=$BQTA1/$RQTA1*$kGpmm_to_Tpm 

param RIRON=500
###define quad magnet 'QTA11'
genericquad QTA11 fieldLength=$LQTA1 apertureRadius=$RQTA1 gradient=$GradQTA1 ironRadius=$RIRON ironLength=$LQTA1 kill=$killInIron ironColor=$ironColor

param PQTA1=$LDRIFT1+0.5*$LQTA1 

# PRoy: the value is PQTA1 = 162 mm (with ProdTarget z @ -958 mm). 162+958 mm = 1120 mm

### place quad magnet 'QTA11'

place QTA11 z=$PQTA1

#*****Define and place Quad magnet named QTB11 with rounded + aperture ****
param LDRIFT2=131

place MuSEDet z=$PQTA1+0.5*$LQTA1+0.5*$LDRIFT2+0.5*$MuSEDetLength rename=DetAQTA1

# DetAQTA1 is place 'A'fter QTA1 and before QTB1, at the mid of the gap between them

param PDRIFT21=$PQTA1+0.5*$LQTA1+0.5*$LDRIFT2

param LQTB=475
param RQTB=115.0

# PRoy (Nov 9, 2018): assume turtle transport model pic from is for pi+.
# QTB1 defocuses pi+ in x, i.e. gradient should be -ve (pg 62, g4beamline manual).

param BQTB1=-1.0894*$scale*$scaleQT

param GradQTB1=$BQTB1/$RQTB*$kGpmm_to_Tpm
genericquad QTB11 gradient=$GradQTB1 fieldLength=$LQTB coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQTB kill=$killInIron ironColor=$ironColor
param PQTB1=$PDRIFT21+0.5*$LDRIFT2+0.5*$LQTB

# PRoy: the value is PQTB1 = 692.5 mm (with Prod target z @ -958 mm). 692.5 + 958 mm = 1650.5 mm
 
place QTB11 z=$PQTB1 

#**** Define and place Quad magnet named QTB12 with rounded + aperture ****
param LDRIFT20=110

place MuSEDet z=$PQTB1+0.5*$LQTB+0.5*$LDRIFT20+0.5*$MuSEDetLength rename=DetAQTB1

# DetAQTB1 is placed 'A'fter QTB1 and before QTB2, at the center of the gap between them

param PDRIFT22=$PQTB1+0.5*$LQTB+0.5*$LDRIFT20

# PRoy (Nov 9, 2018): assume turtle transport model pic from is for pi-.
# QTB2 focuses in pi+ in x, gradient should be +ve

param BQTB2=1.1965*$scale*$scaleQT

param GradBQTB2=$BQTB2/$RQTB*$kGpmm_to_Tpm
genericquad QTB12 gradient=$GradBQTB2 fieldLength=$LQTB coilHalfWidth=50 coilRadius=150 poleTipRadius=125 ironRadius=$RIRON ironLength=$LQTB kill=$killInIron ironColor=$ironColor
param PQTB2=$PDRIFT22+0.5*$LDRIFT20+0.5*$LQTB
place QTB12 z=$PQTB2

#P. Roy: value of PQTB2 = 1277.5 mm (with Prod target z @ -958 mm). 1277.5 + 958 mm = 2235.5 mm

#**** Define and place shield and virtual detector *****************

place MuSEDet z=$PQTB2+0.5*$LQTB+0.5*$MuSEDetLength+500 rename=DetAQTB2

# DetAQTB2 is placed 'A'fter QTB2, at its exit face

param LDRIFT3=3586
param PDRIFT3=$PQTB2+0.5*$LQTB+0.5*$LDRIFT3

param LDRIFT4=1334
param PDRIFT4=$PDRIFT3+0.5*$LDRIFT3+0.5*$LDRIFT4

place SHIELD rename=SHEILD_A z=$PDRIFT4/2-250 length=$PDRIFT4+2500

place MuSEDet z=6384.0-$LPole-0.5*$MuSEDetLength-2 rename=Det1

#**************************************************************************************
# Change beam angle to 75 deg and place next set of beam elements
#**************************************************************************************


param ASMAngleDeg=75.
param ASMAngleRad=$ASMAngleDeg*pi/180.
param L_center_line_ASM=2.*775.39-2*$LPole
param ASMRadius=$L_center_line_ASM/$ASMAngleRad

if "$fieldMap==1"
     cornerarc z=$PDRIFT4+0.5*$LDRIFT4 angle=$ASMAngleDeg/2. centerRadius=$ASMRadius radiusCut=$ASMRadius-160
     param PASM1=$PDRIFT4+0.5*$LDRIFT4+0.5*$L_center_line_ASM
     place SHIELD length=950 z=$PASM1 innerRadius=3*$RIRON outerRadius=3.1*$RIRON initialPhi=-60 finalPhi=60
     #The map is set to 1Tesla so the current/intensity gives the strength of the dipole in Tesla 
     param BASM1=-0.591271616286*155.0/210.0*$scale*$scaleDipole
     place ASM1 z=$PASM1 current=$BASM1
     cornerarc z=$PASM1 angle=$ASMAngleDeg/2. centerRadius=$ASMRadius radiusCut=$ASMRadius-160
else
     #PRoy : value of PASM1 = 7159.4 mm (with Prod target z @ -958 mm). 7159.4 + 958 mm = 8117.4 mm
     
     param PASM1=7159.4
     
     #place SHIELD length=950 z=$PASM1 innerRadius=3*$RIRON outerRadius=3.1*$RIRON initialPhi=-60 finalPhi=60
     # PER 24 Oct 2018  In this case, the bending magnet acts as a shield, so this shield is not necessary
     # and since you have gone all the way around the corner arc, it is off by half the corner arc in rotation
     # and hence interferes with other shield geometry.

     #### Tuning the bending magnet#############

     #******Add reference particle 
     #reference referenceMomentum=$P particle=mu+ beamZ=0

     #*****Tune B field, X-Z position of the bending magnet
     #param -unset Zoffset=0 Xoffset=0

     #tune B1Field z0=6159 z1=8710 initial=-0.591271616286*155.0/210.0*$scale step=0.005 expr=Px1/Pz1 tolerance=0.00001 
     #**** Place the bending magnet
     #place BendType1 rename=ASM1 z=6384.01 By=B1Field

     #*** P Roy (9 Oct 2018) After tuning the magnet's field, I found that B=-0.4363 worked the best to keep
     #### the reference particle at the centerline in all 3 segments. This is a scale factor of 0.99974 to Turtle's value.

     #### End of tuning the bending magnet#############

     #P Roy (Nov 2, 2018):place sextupole magnet at front face of dipole **********
     # to mimic the field of dipole at its faces in Turtle***********************************

     place Pole rename=ASM1_front z=6384.01-0.5*$LPole-1 quadrupole=-0/$RPole*$kGpmm_to_Tpm sextupole=0/($RPole*$RPole)*$kGpmm2_to_Tpm2

     #### Place idealSectorBend magnet ################

     place BendType1 rename=ASM1 z=6384.01 By=-0.99974*0.591271616286*155.0/210.0*$scale
     fieldlines radius=1000 center=0,0,6384 exit=1

     cornerarc z=6384.01 angle=$ASMAngleDeg centerRadius=$L_center_line_ASM/$ASMAngleRad

     #P Roy (Nov 2, 2018):place sextupole magnet at exit face of dipole **********
     # to mimic the field of dipole at its faces in Turtle***********************************

     place Pole rename=ASM1_back z=6384.01+$L_center_line_ASM+0.5*$LPole+1 quadrupole=2/$RPole*$kGpmm_to_Tpm sextupole=1/($RPole*$RPole)*$kGpmm2_to_Tpm2

endif

#********Place shield****************************************************************
place SHIELD rename=sheild_bend z=$PDRIFT4+$L_center_line_ASM length=4000 innerRadius=2.0*$RIRON outerRadius=2.1*$RIRON initialPhi=65 finalPhi=295
param LDRIFT5=1357
param PDRIFT5=$PASM1+0.5*$L_center_line_ASM+0.5*$LDRIFT5
param beginCenter=$PASM1+0.5*$L_center_line_ASM

#****place Det2 at the exit of the first dipole
place MuSEDet z=6384.01+$L_center_line_ASM+$LPole+0.5*$MuSEDetLength+100 rename=Det2

#*****Place quad magnet with name QSL11 and 'round +' type aperture *******************

param BQSL11=-1.2731*$scale*$scaleQSL

param GradBQSL11=$BQSL11/$RQSL*$kGpmm_to_Tpm
param PQSL11=$PDRIFT5+0.5*$LDRIFT5+0.5*$LQSL
place QSL rename=QSL11 gradient=$GradBQSL11 z=$PQSL11

# PRoy : value of PQSL11 = 9586.8 mm (with Prod target z @ -958 mm). 9586.8+958 mm = 10544.8 mm
#*****Place quad magnet with name QSL12 and 'round +' type aperture *******************
param LDRIFT6=112
param PDRIFT6=$PQSL11+0.5*$LQSL+0.5*$LDRIFT6

param BQSL12=0.9086*$scale*$scaleQSL

param GradBQSL12=$BQSL12/$RQSL*$kGpmm_to_Tpm
param PQSL12=$PDRIFT6+0.5*$LDRIFT6+0.5*$LQSL
place QSL rename=QSL12 gradient=$GradBQSL12 z=$PQSL12

# PRoy : value of PQSL12 = 10288 mm (with Prod target z @ -958 mm). 10288+958 mm = 11246 mm

param LDRIFT7=1018-0.5*$MuSEDetLength
param PDRIFT7=$PQSL12+0.5*$LQSL+0.5*$LDRIFT7

#*******Place virtual detector at the IFP and label it as Det3***********************************
#place MuSEDet z=$PDRIFT7+0.5*$LDRIFT7+0.5*$MuSEDetLength rename=Det3

place MuSEDet z=11650 rename=Det3

# PRoy : value of z = 11650 mm (with Prod target z @ -958 mm). 11650+958 mm = 12608 mm

#*****Place quad magnet with name QSL13 and 'round +' type aperture *******************
param LDRIFT8=512

#P Roy: 3 Oct 2018: LDRIFT8 def. changed to match Turtle drift (412 mm + 100 mm (100 mm is to match #IFP width in turtle))

param PDRIFT8=$PDRIFT7+0.5*$MuSEDetLength+0.5*$LDRIFT7
#same as z of IFP

param BQSL13=0.9086*$scale*$scaleQSL

param GradBQSL13=$BQSL13/$RQSL*$kGpmm_to_Tpm
param PQSL13=$PDRIFT8+$LDRIFT8+0.5*$LQSL
place QSL rename=QSL13 gradient=$GradBQSL13 z=$PQSL13

# PRoy : value of PQSL13 = 12408 mm (with Prod target z @ -958 mm). 12408+958 mm = 13366 mm
#*****Place quad magnet with name QSL14 and 'round +' type aperture *******************
param LDRIFT9=112
param PDRIFT9=$PQSL13+0.5*$LQSL+0.5*$LDRIFT9

param BQSL14=-1.2732*$scale*$scaleQSL

param GradBQSL14=$BQSL14/$RQSL*$kGpmm_to_Tpm
param PQSL14=$PDRIFT9+0.5*$LDRIFT9+0.5*$LQSL
place QSL rename=QSL14 gradient=$GradBQSL14 z=$PQSL14

# PRoy : value of PQSL14 = 13110 mm (with Prod target z @ -958 mm). 13110+958 mm = 14068 mm

param LDRIFT10=1311
param PDRIFT10=$PQSL14+0.5*$LQSL+0.5*$LDRIFT10

#*******Place virtual detector and label it as Det4***********************************
param PASM2=$PDRIFT10+0.5*$LDRIFT10+0.5*$L_center_line_ASM
place MuSEDet z=$PASM2-775.39-$LPole-0.5*$MuSEDetLength-2 rename=Det4


#**************************************************************************************
# Change beam angle and place next set of beam elements
#**************************************************************************************
param endCenter=$PDRIFT10+0.5*$LDRIFT10

place SHIELD rename=SHIELD_B z=($endCenter+$beginCenter)/2.0 length=$endCenter-$beginCenter

if "$fieldMap==1"
     cornerarc z=$PDRIFT10+0.5*$LDRIFT10 angle=$ASMAngleDeg/2. centerRadius=$ASMRadius radiusCut=$ASMRadius-160
     #The map is set to 1Tesla so the current/intensity gives the strength of the dipole in Tesla 
     param BASM2=-0.591271616286*155.0/210.0*$scale*$scaleDipole
     place ASM2 z=$PASM2 current=$BASM2  
     cornerarc z=$PASM2 angle=$ASMAngleDeg/2. centerRadius=$ASMRadius radiusCut=$ASMRadius-160
else
     # PRoy : value of PASM2 = 15492.2mm (with Prod target z @ -958 mm). 15492.2+958 mm = 16450.2 mm

     #P Roy (Nov 2, 2018):place multipole magnet at front face of dipole **********
     # to mimic the field corrections to dipole at its faces in Turtle**************

     place Pole rename=ASM1_front z=$PASM2-775.39-0.5*$LPole-1 quadrupole=0/$RPole*$kGpmm_to_Tpm sextupole=0/($RPole*$RPole)*$kGpmm2_to_Tpm2

     #### Place idealSectorBend magnet ################

     place BendType1 rename=ASM2 z=$PASM2-775.39 By=-0.99974*0.591271616286*155.0/210.0*$scale

     cornerarc z=$PASM2-775.39 angle=$ASMAngleDeg centerRadius=$L_center_line_ASM/$ASMAngleRad

     #P Roy (Nov 2, 2018):place multipole magnet at exit face of dipole **********
     # to mimic the field corrections to dipole at its faces in Turtle*************

     place Pole rename=ASM1_back z=$PASM2-775.39+$L_center_line_ASM-0.5*$LPole+1 quadrupole=2/$RPole*$kGpmm_to_Tpm sextupole=0/($RPole*$RPole)*$kGpmm2_to_Tpm2

endif

#*******Place virtual detector and label it as Det5***********************************
place MuSEDet z=$PASM2-775.39+$L_center_line_ASM+$LPole+0.5*$MuSEDetLength+2 rename=Det5

#*****Place quad magnet with name QSL15 and 'round +' type aperture *******************
param LDRIFT11=684

param BQSL15=0.5440*$scale*$scaleQSL

param GradBQSL15=$BQSL15/$RQSL*$kGpmm_to_Tpm
#place QSL rename=QSL15 z=2940-1452/2.0-$LQSL/2+$PASM2

#P.Roy: 3 Oct 2018: corrected def for position of QSL15 and defined B field gradient

param PQSL15=$PASM2+0.5*$L_center_line_ASM+$LDRIFT11+0.5*$LQSL 
place QSL rename=QSL15 gradient=$GradBQSL15 z=$PQSL15

# PRoy : value of PQSL15 = 17246.6 mm (with Prod target z @ -958 mm).17246.6 +958 mm = 18204.6 mm

#*****Place quad magnet with name QSL16 and 'round +' type aperture *******************
param BQSL16=-0.7506*$scale*$scaleQSL

param GradBQSL16=$BQSL16/$RQSL*$kGpmm_to_Tpm
# place QSL rename=QSL16 gradient=$GradBQSL16 z=2940-$LQSL/2+$PASM2

#P.Roy: 3 Oct 2018: corrected def for position of QSL16
param LDRIFT12=112
param PQSL16=$PQSL15+$LQSL+$LDRIFT12

place QSL rename=QSL16 gradient=$GradBQSL16 z=$PQSL16

# PRoy : value of PQSL16 = 17948.6 mm (with Prod target z @ -958 mm).17948.6 +958 mm = 18906.6 mm

#*****Place quad magnet with name QSL17 and 'round +' type aperture *******************

param BQSL17=0.9086*$scale*$scaleQSL

param GradBQSL17=$BQSL17/$RQSL*$kGpmm_to_Tpm
#place QSL rename=QSL17 gradient=$GradBQSL17 z=4231.25+$PASM2

#P.Roy: 3 Oct 2018: corrected def for position of QSL17
param LDRIFT13=1038
param PQSL17=$PQSL16+$LQSL+$LDRIFT13

place QSL rename=QSL17 gradient=$GradBQSL17 z=$PQSL17

# PRoy : value of PQSL17 = 19576.6 mm (with Prod target z @ -958 mm).19576.6 +958 mm = 20534.6 mm

#*****Place quad magnet with name QSL18 and 'round +' type aperture *******************

param BQSL18=-1.2732*$scale*$scaleQSL

param GradBQSL18=$BQSL18/$RQSL*$kGpmm_to_Tpm
#place QSL rename=QSL18 gradient=$GradBQSL18 z=4957.75+$PASM2

#P.Roy: 3 Oct 2018: corrected def for position of QSL17
param LDRIFT14=163
param PQSL18=$PQSL17+$LQSL+$LDRIFT14

place QSL rename=QSL18 gradient=$GradBQSL18 z=$PQSL18

# PRoy : value of PQSL18 = 20329.6 mm (with Prod target z @ -958 mm).20329.6 +958 mm = 21287.6 mm

#************************************************************************************************
#param targetLocation=6802.678+$PASM2

#P.Roy: 3 Oct 2018: corrected def for position of target to match turtle
param LDRIFT15=1536
param targetLocation=22159.5

# PRoy:value of targetLocation=22159.5 mm (with Prod target z@-958 mm). 22159.5+958 mm = 23117.5 mm

place MuSEDet z=$targetLocation-1000 rename=DetBH

place MuSEDet z=$targetLocation rename=DetTar

place SHIELD rename=SHIELD_TAR z=$targetLocation innerRadius=2*$MuSEDetRadius outerRadius=2.1*$MuSEDetRadius length=5000

place MuSEDet z=$targetLocation+1500 rename=DetBC1

place MuSEDet z=$targetLocation+2500 rename=DetBC2

ntuple partOut detectors=Det* require=DetTar
